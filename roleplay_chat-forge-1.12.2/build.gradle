buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

configurations {
    shade
    compile.extendsFrom shade
}

minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion

    replaceIn "RoleplayChat.java"
    replace "@MODID@", modBaseName
    replace "@VERSION@", forgeVersion
}

dependencies {
    compile project(":roleplay_chat-common")

    testCompile 'org.testng:testng:6.10'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

